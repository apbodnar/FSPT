<!DOCTYPE html>
<html>
<head>
<title>Ping Pong Blinn Phong</title>
<style>
  body{ text-align: center; margin: 0; overflow: hidden; background: #000;}
  canvas{ border: none; }
</style>
<script id="FRAGMENT_SHADER" type="x-shader/x-fragment">
  precision highp float;
  const int sphereCount = 13;
  const float max_t = 100000.0;
  varying vec2 coords;
  uniform int mode;
  uniform int tick;
  uniform vec2 dims;
  uniform vec3 eye;
  uniform vec3 spherePositions[sphereCount];
  uniform vec3 sphereAttrs[sphereCount];
  uniform vec3 sphereMats[sphereCount];
  uniform vec3 sphereColors[sphereCount];
  uniform sampler2D fbTex;
  uniform sampler2D noiseTex;

  struct Sphere {
    vec3 pos;
    vec3 attrs;
    vec3 color;
    vec3 material;
  };
  struct Hit{
    float t;
    vec3 pos;
    vec3 dir;
    vec3 normal;
    vec3 emmittance;
    vec3 BRDF;
    int index;
  };
  struct Ray{
    vec3 origin;
    vec3 dir;
  };

  float rand(vec2 co){
    float a = 12.9898;
    float b = 78.233;
    float c = 43758.5453;
    float dt= dot(co ,vec2(a,b));
    float sn= mod(dt,3.14);
    return fract(sin(sn) * c) - 0.5;
  }

  vec3 randomVec(int index, vec3 normal, vec3 pos){
    vec3 tc = vec3(rand(pos.xy+float(tick)*0.1329),rand(pos.xz+float(tick)*0.7347),rand(pos.yz+float(tick)*0.3391));
    return normalize(dot(tc,normal) > 0.0 ? tc : -tc); 
  }

  // vec3 randomVec(int index, vec3 normal, vec3 pos){
  //   float x = mod(coords.x + pos.x + (float(tick)*33.1234)/float(dims.x), 1.0);
  //   float y = mod(coords.y + pos.y + (float(tick)*41.7653)/float(dims.y), 1.0);
  //   vec3 tc = normalize(texture2D(noiseTex,vec2(x, y)).rgb - 0.5);
  //   return dot(tc,normal) > 0.0 ? tc : -tc; 
  // }

  float checkSphereCollision(Ray ray,Sphere s){
    float scalar = dot(ray.dir,ray.origin - s.pos);
    float dist = distance(ray.origin, s.pos);
    float squared = (scalar * scalar) - (dist * dist) + (s.attrs.r * s.attrs.r);
    return squared < 0.0 ? max_t : -scalar - sqrt(squared);
  }

  Hit getCollision(Ray ray, int current){
    float t = max_t;
    Hit result = Hit(max_t,vec3(0,0,0),vec3(0,0,0),vec3(0,0,0),vec3(0,0,0),vec3(0,0,0),-1);
    for(int i=0; i<sphereCount; i++){
      Sphere s = Sphere(spherePositions[i],sphereAttrs[i],sphereColors[i],sphereMats[i]);
      float radius = s.attrs.r;
      float nt = checkSphereCollision(ray,s);
      if(nt < result.t && nt > 0.0 && current != i){
        result.t = nt;
        result.pos = ray.dir*nt + ray.origin;
        result.normal = normalize(result.pos - s.pos);
        vec3 ref = (s.material.r > 1.0) ? reflect(ray.dir, result.normal) : result.normal;
        result.dir = randomVec(i, result.normal, result.pos);
        result.emmittance = s.attrs.z * s.color;
        result.BRDF = 2.0 * s.attrs.y * s.color * dot(result.dir,(result.normal+ref)*0.5);
        result.index = i;
      }
    }
    return result;
  }

  void main(void) {
    vec3 tcolor = texture2D(fbTex,coords*0.5 + 0.5).rgb;
    if(mode == 0){
      vec3 origin = vec3(coords.x*dims.x/dims.y,coords.y,0);
      Ray ray = Ray(origin,normalize(origin - eye));
      int index = -1;
      vec3 color = vec3(0,0,0);
      //No recursion in GLSL
      Hit r0 = getCollision(ray,index);
      Hit r1 = getCollision(Ray(r0.pos,r0.dir),r0.index);
      Hit r2 = getCollision(Ray(r1.pos,r1.dir),r1.index);
      Hit r3 = getCollision(Ray(r2.pos,r2.dir),r2.index);
      Hit r4 = getCollision(Ray(r3.pos,r3.dir),r3.index);
      color = r0.emmittance + r0.BRDF *(r1.emmittance + r1.BRDF * (r2.emmittance + r2.BRDF *(r3.emmittance + r3.BRDF * (r4.emmittance))));
      gl_FragColor = abs(vec4((color + (tcolor * float(tick)))/float(tick+1),1.0));
    } else {
      gl_FragColor = abs(vec4(tcolor,1.0));
    }
  }
</script>

<script id="VERTEX_SHADER" type="x-shader/x-vertex">
  precision highp float;
  attribute vec3 corner;
  varying vec2 coords;
  void main(void) {
    coords = corner.xy;
    gl_Position = vec4(corner, 1.0);
  }
</script>
<script src="fspt.js" type="text/javascript"></script>
</head>


<body onload="webGLStart();">
  <canvas id="trace" style="border: none;"></canvas>
</body>

</html>
