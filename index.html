<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8">
<title>Blinn Phong Ping Pong</title>
<style>
  body{ text-align: center; margin: 0; overflow: hidden; background: #000;}
  canvas{ border: none; }
</style>
<script id="FRAGMENT_SHADER" type="x-shader/x-fragment">
  precision mediump float;
  const int sphereCount = 15;
  const float max_t = 100000.0;
  const float n1 = 1.0;
  const float n2 = 1.458;
  const float sr = n1/n2;
  const float r0 = ((n1 - n2)/(n1 + n2))*((n1 - n2)/(n1 + n2));
  varying vec2 coords;
  uniform int mode;
  uniform int tick;
  uniform vec2 dims;
  uniform vec3 eye;
  uniform vec3 spherePositions[sphereCount];
  uniform vec3 sphereAttrs[sphereCount];
  uniform vec3 sphereMats[sphereCount];
  uniform vec3 sphereColors[sphereCount];
  uniform sampler2D fbTex;

  struct Sphere {
    vec3 pos;
    vec3 attrs;
    vec3 color;
    vec3 material;
  };
  struct Hit{
    float t;
    vec3 pos;
    vec3 dir;
    vec3 normal;
    vec3 emmittance;
    vec3 BRDF;
    int index;
  };
  struct Ray{
    vec3 origin;
    vec3 dir;
  };

  float rand(vec2 co){
    float a = 12.9898;
    float b = 78.233;
    float c = 43758.5453;
    float dt= dot(co ,vec2(a,b));
    float sn= mod(dt,3.14);
    return fract(sin(sn) * c) - 0.5;
  }

  vec3 randomVec(int index, vec3 normal, vec3 pos){
    vec3 tc = vec3(rand(pos.xy+float(tick)*0.01329),rand(pos.xz+float(tick)*0.07347),rand(pos.yz+float(tick)*0.003391));
    return normalize(dot(tc,normal) > 0.0 ? tc : -tc);
  }

  float checkSphereCollision(Ray ray,Sphere s){
    float scalar = dot(ray.dir,ray.origin - s.pos);
    float dist = distance(ray.origin, s.pos);
    float squared = (scalar * scalar) - (dist * dist) + (s.attrs.r * s.attrs.r);
    return squared < 0.0 ? max_t : -scalar - sqrt(squared);
  }

  Hit getCollision(Ray ray, int current){
    float t = max_t;
    Hit result = Hit(max_t,vec3(0,0,0),vec3(0,0,0),vec3(0,0,0),vec3(0,0,0),vec3(0,0,0),-1);
    for(int i=0; i<sphereCount; i++){
      Sphere s = Sphere(spherePositions[i],sphereAttrs[i],sphereColors[i],sphereMats[i]);
      float radius = s.attrs.r;
      float nt = checkSphereCollision(ray,s);
      if(nt < result.t && nt > 0.0 && current != i){
        result.t = nt;
        result.pos = ray.dir*nt + ray.origin;
        result.normal = normalize(result.pos - s.pos);
        result.emmittance = s.attrs.z * s.color;
        result.index = i;
        if( int(s.material.z) == 0 ){ //diffuse
          result.dir = randomVec(i, result.normal, result.pos);
          result.BRDF = 2.0 * s.attrs.y * s.color * dot(result.dir,result.normal);
        } else if( int(s.material.z) == 1 ){ //transmissive
          float dh = 1.0 - dot(-ray.dir,result.normal);
          float re = r0 + (1.0 - r0)*dh*dh*dh*dh*dh;
          if(rand(result.pos.xy + float(tick)*0.0017839) + 0.5 < re){
            result.dir = reflect(ray.dir, result.normal);
          }else{
            float c = dot(ray.dir,-result.normal);
            vec3 ref = normalize(sr*ray.dir + (sr*c - sqrt(1.0 - sr*sr*(1.0 - c*c)))*result.normal);
            result.pos = ref*dot(ref,result.normal)*radius*2.0 + result.pos;
            result.dir = reflect(-ray.dir,ref);
          }
          result.BRDF = vec3(1);
        }
      }
    }
    return result;
  }

  void main(void) {
    vec3 tcolor = texture2D(fbTex,coords*0.5 + 0.5).rgb;
    if(mode == 0){
      vec2 dof = vec2(rand(coords-float(tick)*0.02571)*0.01,rand(coords.yx+float(tick)*0.005831)*0.01);
      vec3 origin = vec3(coords.x*dims.x/dims.y,coords.y,0);
      Ray ray = Ray(origin- vec3(dof.y,dof.x,0),normalize((origin - eye) + vec3(dof.y*2.0,dof.x*2.0,0)));
      int index = -1;
      vec3 color = vec3(0,0,0);
      //No recursion in GLSL
      Hit r0 = getCollision(ray,index);
      Hit r1 = getCollision(Ray(r0.pos,r0.dir),r0.index);
      Hit r2 = getCollision(Ray(r1.pos,r1.dir),r1.index);
      Hit r3 = getCollision(Ray(r2.pos,r2.dir),r2.index);
      Hit r4 = getCollision(Ray(r3.pos,r3.dir),r3.index);
      color = (r0.emmittance + r0.BRDF *
              (r1.emmittance + r1.BRDF * 
              (r2.emmittance + r2.BRDF *
              (r3.emmittance + r3.BRDF * 
              (r4.emmittance)))));
      gl_FragColor = abs(vec4((color + (tcolor * float(tick)))/float(tick+1),1.0));
    } else {
      gl_FragColor = abs(vec4(tcolor,1.0));
    }
  }
</script>

<script id="VERTEX_SHADER" type="x-shader/x-vertex">
  precision mediump float;
  attribute vec3 corner;
  varying vec2 coords;
  void main(void) {
    coords = corner.xy;
    gl_Position = vec4(corner, 1.0);
  }
</script>
<script src="fspt.js" type="text/javascript"></script>
</head>


<body onload="webGLStart();">
  <canvas id="trace" style="border: none;"></canvas>
</body>

</html>
